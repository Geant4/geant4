*---------------------------------------------------------
* This kumac requires 1 input parameter, which is the ID
* of the histogram to plot.
* This kumac plots in  red  the specified histogram from
* the file  histo_a.hbook  and overlaps to it the same
* histogram in  blue  from the file  histo_b.hbook,
* in both linear and logarithmic scale.
* The it does the same by zooming in, leaving 1% in the
* right tail, and eventually 1% in the left tail in the
* case of a long left tail.
* The two plots are then saved in the file  plot.ps  
*---------------------------------------------------------
*
histo/file 1 histo_a.hbook
histo/file 2 histo_b.hbook
*
for/file 20 plot.ps
meta 20 -111
zone 1 2
set dmod 1
*
set hcol 2
histo/plot //lun1/[1]
set hcol 4
histo/plot //lun2/[1] s
*
opt logy
*
set hcol 2
histo/plot //lun1/[1]
set hcol 4
histo/plot //lun2/[1] s
*
*wait
*
*--- Zoom --
*
vec/create v(500)
histo/get/cont //lun1/[1] v
SIGMA N=VSUM(v)
SIGMA sv=SUMV(v)
* 
* Determine the following index positions of the vector v :
*   -  I_L   where   1% of the data lies on the left.
*   -  I_R   where  99% of the data lies on the left.
*   -  I100  where 100% of the data lies on the left.
SIGMA N5=N*0.01
I_L=0
I_R=0
I100=0
DO I=1,$VDIM(sv,1)
 IF ( sv([I]) < N5 ) THEN
    I_L=[I]
 ENDIF
 IF ( sv([I]) < N-N5 ) THEN
    I_R=[I]
 ENDIF
 IF ( v([I]) > 0 ) THEN
    I100=[I]
 ENDIF
ENDDO
*
message  I_L=[I_L] , I_R=[I_R] , I100=[I100]
*
* Zoom, by leaving out the left tail and/or the right tail
* if, respectively, the positions I_L is more than 10% away
* from the first bin, and I_R is more than 10% away from the
* last bin, where 10% refers to the total number of bins 
IMIN = 1
IF ( [I_L] > [I100]*0.10 ) THEN
  IMIN = [I_L]
ENDIF
*
IMAX = [I100]
IF ( $RSIGMA([I100]-[I_R]) > $RSIGMA([I100]*0.10) ) THEN
  IMAX = [I_R]
ENDIF
*
message IMIN = [IMIN] , IMAX = [IMAX]
*
IF ( [IMIN] < [IMAX] .AND. ( [IMIN] > 1 .OR. [IMAX] < [I100] ) ) THEN
  vec/create xvec([I100])
  histo/get/abs [1] xvec
  BSIZ = $RSIGMA( xvec(2) - xvec(1) )
  NBIN = $SIGMA( [IMAX] - [IMIN] + 1 )
  XMIN = $RSIGMA( xvec([IMIN]) - 0.5*[BSIZ] )
  XMAX = $RSIGMA( xvec([IMAX]) + 0.5*[BSIZ] )
*
  1dhisto 69690 'zoom A' [NBIN] [XMIN] [XMAX]
  vec/copy v([IMIN]:[IMAX]) vv
  histo/put/cont 69690 vv
*
  vec/create w(500)
  histo/get/cont //lun2/[1] w
  1dhisto 69691 'zoom B' [NBIN] [XMIN] [XMAX]
  vec/copy w([IMIN]:[IMAX]) ww
  histo/put/cont 69691 ww
*  
  title 'ZOOM'
  opt liny
*
  set hcol 2
  histo/plot 69690
  set hcol 4
  histo/plot 69691 s
*
  opt logy
*
  set hcol 2
  histo/plot 69690
  set hcol 4
  histo/plot 69691 s
*
  vec/del xvec
  vec/del vv
  vec/del w
  vec/del ww
  histo/del 69690
  histo/del 69691
*
ENDIF
*
close 20
*
opt liny
*
vec/del v
vec/del sv
close 1
close 2
*
